apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

buildscript {
    def kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.get('kotlinVersion') : project.properties['RNVkotlinVersion']

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

def getExtOrDefault(name, defaultValue) {
    rootProject.ext.has(name) ? rootProject.ext.get(name) : defaultValue
}

def isNewArchitectureEnabled() {
    // To opt-in for the New Architecture, you can either:
    // - Set `newArchEnabled` to true inside the `gradle.properties` file
    // - Invoke gradle with `-newArchEnabled=true`
    // - Set an environment variable `ORG_GRADLE_PROJECT_newArchEnabled=true`
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

def useExoplayerIMA = getExtOrDefault("RNVUseExoplayerIMA", project.properties['RNVUseExoplayerIMA'])

println "useExoplayerIMA:" + useExoplayerIMA

// This string is used to define build path.
// As react native build output directory is react-native path of the module.
// We need to force a new path on each configuration change.
// If you add a new build parameter, please add the new value in this string
def configStringPath = (
        'useExoplayerIMA' + useExoplayerIMA \
).md5()

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}

android {
    compileSdkVersion getExtOrDefault('compileSdkVersion', project.properties['RNVcompileSdkVersion'])
    buildToolsVersion getExtOrDefault('buildToolsVersion', project.properties['RNVbuildToolsVersion'])

    defaultConfig {
        minSdkVersion getExtOrDefault('minSdkVersion', project.properties['RNVminSdkVersion'])
        targetSdkVersion getExtOrDefault('targetSdkVersion', project.properties['RNVtargetSdkVersion'])
        versionCode 1
        versionName "1.0"
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()

        ndk {
            abiFilters(*reactNativeArchitectures())
        }
    }

    packagingOptions {
        exclude "**/libreact_render*.so"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=all']
        jvmTarget = JavaVersion.VERSION_11.majorVersion
   }

    buildDir 'buildOutput_' + configStringPath

    sourceSets {
        main {
            java {
                if (useExoplayerIMA) {
                    exclude 'com/google/ads/interactivemedia/v3/api'
                    exclude 'com/google/android/exoplayer2/ext/ima'
                }
            }
        }
    }

    sourceSets.main {
        java {
            if (isNewArchitectureEnabled()) {
                srcDirs += [
                    "src/fabric/java",
                    "${project.buildDir}/generated/source/codegen/java"
                ]
            } else {
                srcDirs += [
                    "src/oldarch/java"
                ]
            }
        }
    }
}

def reactNativeArchitectures() {
    def value = project.getProperties().get("reactNativeArchitectures")
    return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

repositories {
    google()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
    mavenCentral()
}

def kotlin_version = getExtOrDefault('kotlinVersion', project.properties['RNVkotlinVersion'])

dependencies {
    implementation "com.facebook.react:react-native:${getExtOrDefault('reactNativeVersion', '+')}"
    implementation('com.google.android.exoplayer:exoplayer:2.18.1') {
        exclude group: 'com.android.support'
    }

    // All support libs must use the same version
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.core:core:1.1.0"
    implementation "androidx.media:media:1.1.0"
    implementation "androidx.activity:activity:1.4.0"

    implementation('com.google.android.exoplayer:extension-okhttp:2.18.1') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }

    if (useExoplayerIMA) {
        implementation 'com.google.android.exoplayer:extension-ima:2.18.1'
    }
    implementation "com.squareup.okhttp3:okhttp:" + '$OKHTTP_VERSION'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
